plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.0'
    id 'de.marcphilipp.nexus-publish' version '0.2.0'
}

group 'com.slickqa'
version '1.0.0-' + projectBuildNumber
archivesBaseName = "slickqa-junit-jupiter"

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    //testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    compile 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
    compile 'org.junit.jupiter:junit-jupiter-params:5.4.2'
    compile 'org.junit.platform:junit-platform-launcher:1.4.2'
    compile 'com.slickqa:slickqa-java-client:1.0.3-6'
    testCompile 'org.mongodb:bson:3.10.2'
}

test {
    useJUnitPlatform {
        filter {
            excludeTestsMatching 'com.slickqa.jupiter.e2e.*'
        }
    }
}

task e2eTest(type: Test) {
    useJUnitPlatform {
        filter {
            includeTestsMatching 'com.slickqa.jupiter.e2e.tests.*'
        }
    }
}

task javadocJar(type: Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'slickqa-junit-jupiter'
                description = 'Junit Jupiter extension that reports results to slick'
                url = 'https://github.com/slickqa/slickqa-junit-jupiter'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'leeard'
                        name = 'Lee Higginson'
                        email = 'leeard@gmail.com'
                    }
                    developer {
                        id = 'jasoncorbett'
                        name = 'Jason Corbett'
                        email = 'jasoncorbett@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/slickqa/slickqa-junit-jupiter.git'
                    developerConnection = 'scm:git:https://github.com/slickqa/slickqa-junit-jupiter.git'
                    url = 'https://github.com/slickqa/slickqa-junit-jupiter'
                }
            }
        }
    }
}

signing {
    required { isReleaseVersion }
    sign publishing.publications.mavenJava
}

nexusStaging {
    packageGroup = 'com.slickqa'
    username = ossrhUsername
    password = ossrhPassword
}

nexusPublishing {
    username = ossrhUsername // defaults to project.properties["nexusUsername"]
    password = ossrhPassword // defaults to project.properties["nexusPassword"]
}


